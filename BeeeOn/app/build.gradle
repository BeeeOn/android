apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'android-apt'

project.ext.set("versionName", "1.8.0")
project.ext.set("supportLibVersion", "24.2.1")
project.ext.set("playServicesVersion", "9.6.1")

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.rehivetech.beeeon"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 80
        versionName project.versionName
        signingConfig signingConfigs.debug
        resConfigs "en", "cs", "sk"
    }

    signingConfigs {
        debug {
            storeFile file('../keystores/debug.keystore')
        }
        release {
            storeFile file('../keystores/release.keystore')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            versionNameSuffix " debug"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "BeeeOn Debug"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "BeeeOn Beta"
        }

        presentation {
            debuggable true
            signingConfig signingConfigs.debug
            versionNameSuffix " demo"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "BeeeOn Demo"
        }
    }
    productFlavors {
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.getBuildType().isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        into "${rootDir}/mapping"
                        rename { String fileName ->
                            "${project.versionName}.txt"
                        }
                    }
                }
            }
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    maven { url "https://jitpack.io" } // MP Android Chart
    maven { url "https://clojars.org/repo/" }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' } // Stetho realm
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Support libraries
    compile "com.android.support:support-v4:${project.ext.supportLibVersion}"
    compile "com.android.support:appcompat-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:design:${project.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:preference-v7:${project.ext.supportLibVersion}"
    // Google Play libraries
    compile "com.google.android.gms:play-services-analytics:${project.ext.playServicesVersion}"
    compile "com.google.android.gms:play-services-identity:${project.ext.playServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${project.ext.playServicesVersion}"
    compile "com.google.android.gms:play-services-gcm:${project.ext.playServicesVersion}"
    compile "com.google.android.gms:play-services-maps:${project.ext.playServicesVersion}"
    // UI libraries
    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.avast:android-styled-dialogs:2.3.3'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile 'com.github.clans:fab:1.6.4'
    compile 'me.dm7.barcodescanner:zxing:1.8.4'
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    // tools (db, binding, state, ..)
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'joda-time:joda-time:2.7'
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
    compile 'com.facebook.android:facebook-android-sdk:4.12.1'
    compile 'com.google.code.gson:gson:2.6.1'
    compile 'io.realm:android-adapters:1.2.2'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    // DEBUG ONLY
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.uphyca:stetho_realm:0.9.0'
}
