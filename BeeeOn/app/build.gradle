apply plugin: 'com.android.application'

project.ext.set("versionName", "1.6.2")
project.ext.set("supportLibVersion", "23.2.0")
project.ext.set("playServicesVersion", "8.4.0")

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.2"
	defaultConfig {
		applicationId "com.rehivetech.beeeon"
		minSdkVersion 15
		targetSdkVersion 23
		versionCode 75
		versionName project.versionName
		signingConfig signingConfigs.debug
		resConfigs "en", "cs", "sk"
	}

	signingConfigs {
		debug {
			storeFile file('../keystores/debug.keystore')
		}
		release {
			storeFile file('../keystores/release.keystore')
			storePassword System.getenv("KEYSTORE_PASSWORD")
			keyAlias System.getenv("KEY_ALIAS")
			keyPassword System.getenv("KEY_PASSWORD")
		}
	}

	buildTypes {
		debug {
			debuggable true
			signingConfig signingConfigs.debug
			versionNameSuffix " debug"
			applicationIdSuffix ".debug"
			resValue "string", "app_name", "BeeeOn Debug"
		}

		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			resValue "string", "app_name", "BeeeOn Beta"
		}
	}
	productFlavors {
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			if (variant.getBuildType().isMinifyEnabled()) {
				variant.assemble.doLast{
					copy {
						from variant.mappingFile
						into "${rootDir}/mapping"
						rename { String fileName ->
							"${project.versionName}.txt"
						}
					}
				}
			}
		}
	}
}

repositories {
	flatDir { dirs 'libs' }
	maven { url "https://jitpack.io" } // MP Android Chart
	mavenCentral()
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	// Support libraries
	compile "com.android.support:support-v4:${project.ext.supportLibVersion}"
	compile "com.android.support:appcompat-v7:${project.ext.supportLibVersion}"
	compile "com.android.support:recyclerview-v7:${project.ext.supportLibVersion}"
	compile "com.android.support:design:${project.ext.supportLibVersion}"
	compile "com.android.support:cardview-v7:${project.ext.supportLibVersion}"
	// Google Play libraries
	compile "com.google.android.gms:play-services-analytics:${project.ext.playServicesVersion}"
	compile "com.google.android.gms:play-services-identity:${project.ext.playServicesVersion}"
	compile "com.google.android.gms:play-services-gcm:${project.ext.playServicesVersion}"
	// Third-party libraries
	compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
	compile 'joda-time:joda-time:2.7'
	compile 'com.avast:android-styled-dialogs:2.3.3'
	compile 'com.facebook.android:facebook-android-sdk:4.5.0'
	compile 'com.github.PhilJay:MPAndroidChart:v2.2.2'
	compile 'com.github.shkschneider:android_PreferenceFragmentCompat:1.0.0@aar'
	compile 'com.github.clans:fab:1.6.2'
	compile 'com.google.code.gson:gson:2.6.1'
}
