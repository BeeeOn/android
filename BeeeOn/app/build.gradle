apply plugin: 'com.android.application'

project.ext.set("versionName", "1.5.1")

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.2"
	defaultConfig {
		applicationId "com.rehivetech.beeeon"
		minSdkVersion 10
		targetSdkVersion 23
		versionCode 70
		versionName project.versionName
		signingConfig signingConfigs.debug
		resConfigs "en", "cs", "sk"
	}

	signingConfigs {
		debug {
			storeFile file('../keystores/debug.keystore')
		}
	}

	buildTypes {
		debug {
			debuggable true
			signingConfig signingConfigs.debug
			versionNameSuffix " debug"
			applicationIdSuffix ".debug"
            resValue "string", "app_name", "BeeeOn Debug"
		}

		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			resValue "string", "app_name", "BeeeOn Beta"
		}
	}
	productFlavors {
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			if (variant.getBuildType().isMinifyEnabled()) {
				variant.assemble.doLast{
					copy {
						from variant.mappingFile
						into "${rootDir}/mapping"
						rename { String fileName ->
							"${project.versionName}.txt"
						}
					}
				}
			}
		}
	}
}

repositories {
	flatDir { dirs 'libs' }
	maven { url "https://jitpack.io" } // MP Android Chart
	mavenCentral()
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	// Support libraries
	compile 'com.android.support:support-v4:23.1.1'
	compile 'com.android.support:appcompat-v7:23.1.1'
	compile 'com.android.support:recyclerview-v7:23.1.1'
	compile 'com.android.support:design:23.1.1'
	compile 'com.android.support:cardview-v7:23.1.1'
	// Google Play libraries
	compile 'com.google.android.gms:play-services-analytics:8.3.0'
	compile 'com.google.android.gms:play-services-identity:8.3.0'
	compile 'com.google.android.gms:play-services-gcm:8.3.0'
	// Third-party libraries
	compile 'ue.rekisoft.android.numberpicker:android-numberpicker@aar'
	compile 'net.i2p.android.ext:floatingactionbutton:1.10.0'
	compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
	compile 'joda-time:joda-time:2.7'
	compile 'com.github.lzyzsd:circleprogress:1.1.0@aar'
	compile 'com.avast:android-styled-dialogs:2.2.0'
	compile 'com.facebook.android:facebook-android-sdk:4.5.0'
	compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
	compile 'com.github.shkschneider:android_PreferenceFragmentCompat:1.0.0@aar'
}
